apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.gladed.androidgitversion'
apply from: '../deployment/signin.gradle'

androidGitVersion {
    codeFormat 'MNNPPP'
    format "%tag%%-dirty%"
    prefix 'v'
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.payproapp"
        minSdkVersion 21
        targetSdkVersion 28
        versionName androidGitVersion.name()
        versionCode androidGitVersion.code()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField "String", "BASE_URL", "\"http://putYourAddressHere\""
        buildConfigField "String", "FAKE_USERNAME", "\"Apk_Paypr0\""
        buildConfigField "String", "FAKE_PASSWORD", "\"\""
    }

    dataBinding {
        enabled = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    //Kotlin version
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    def lifecycle_version = "2.0.0"
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    // ViewModel and LiveData
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    kapt "androidx.lifecycle:lifecycle-compiler:$lifecycle_version"

    //Dagger
    implementation "com.google.dagger:dagger:2.16"
    implementation 'com.google.dagger:dagger-android:2.16'
    implementation 'com.google.dagger:dagger-android-support:2.16'
    kapt "com.google.dagger:dagger-compiler:2.16"
    kapt 'com.google.dagger:dagger-android-processor:2.16'

    //Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.4.0'
    implementation "com.squareup.retrofit2:converter-gson:2.4.0"
    implementation 'com.squareup.retrofit2:converter-scalars:2.4.0'

    //Facebook
    implementation "com.facebook.stetho:stetho:1.5.0"
    implementation "com.facebook.stetho:stetho-okhttp3:1.5.0"
    implementation "com.squareup.okhttp3:logging-interceptor:3.11.0"

    //Rx
    implementation 'io.reactivex.rxjava2:rxjava:2.2.3'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.2.0'

    //third parties
    implementation 'pub.devrel:easypermissions:2.0.0'
    implementation "com.jakewharton.timber:timber:4.7.1"
    implementation 'io.intercom.android:intercom-sdk-base:5.0.0'
    implementation 'com.google.zxing:core:3.3.0'
    implementation 'org.jetbrains.anko:anko-common:0.10.7'
    implementation 'com.journeyapps:zxing-android-embedded:3.6.0'

    //Testing
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'


}


android.applicationVariants.all { variant ->
    variant.outputs.all {
        outputFileName =
                "Infinit-${variant.flavorName ? variant.flavorName + "-" : ""}${variant.buildType.name}-${variant.versionName}.apk"
    }
}
